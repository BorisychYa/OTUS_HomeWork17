///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2024, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
#Если ВебКлиент ИЛИ МобильныйКлиент Тогда
	ПоказатьПредупреждение(, НСтр("ru = 'Запуск в веб-клиенте или в мобильном клиенте невозможен.
		|Запустите тонкий клиент.';
		|en = 'Cannot start in web client or mobile client.
		|Start thin client.'"));
	Отказ = Истина;
	Возврат;
#КонецЕсли
	
	ПараметрыЗапускаЧастями = СтрРазделить(ПараметрЗапуска, ";", Ложь);
	Для Каждого Параметр Из ПараметрыЗапускаЧастями Цикл
		Если СтрНайти(Параметр, "ФайлОбновления") > 0 Тогда
			ФайлОбновления  = СокрЛП(СтрРазделить(Параметр, "=")[1]);
			ФайлОбновления = СтрЗаменить(ФайлОбновления, """", "");
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ФайлОбновления) Тогда
		ДвоичныеДанные = Новый ДвоичныеДанные(ФайлОбновления);
		Хранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
		ОбновитьНаИсправительнуюВерсиюНаСервере(Хранилище);
		ЗавершитьРаботуСистемы(Ложь, Ложь);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СформироватьФайлНастроек(Команда)
	
	ПараметрыСохранения = ФайловаяСистемаКлиент.ПараметрыСохраненияФайла();
	ПараметрыСохранения.Диалог.Заголовок  = НСтр("ru = 'Укажите имя файла настроек сравнения/объединения';
												|en = 'Specify a name of the comparison or merging setting file'");
	ПараметрыСохранения.Диалог.Фильтр     = НСтр("ru = 'Файл настроек сравнения/объединения (*.xml)|*.xml';
												|en = 'File of comparison or merging settings (*.xml)|*.xml'");
	ПараметрыСохранения.Диалог.Расширение = "xml";
	ФайловаяСистемаКлиент.СохранитьФайл(Неопределено, СформироватьФайлНастроекНаСервере(), "settings.xml", ПараметрыСохранения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНаИсправительнуюВерсию(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ОбновитьНаИсправительнуюВерсиюПродолжение", ЭтотОбъект);
	ПараметрыЗагрузки = ФайловаяСистемаКлиент.ПараметрыЗагрузкиФайла();
	ПараметрыЗагрузки.ИдентификаторФормы = УникальныйИдентификатор;
	ПараметрыЗагрузки.Диалог.Фильтр = НСтр("ru = 'Конфигурация информационной базы 1С:Предприятия 8 (*.cf)|*.cf';
											|en = '1C:Enterprise 8 infobase configuration (*.cf)|*.cf'");
	ПараметрыЗагрузки.Диалог.МножественныйВыбор = Ложь;
	ПараметрыЗагрузки.Диалог.Заголовок = НСтр("ru = 'Укажите файл новой версии БСП';
												|en = 'Select file with new SSL version'");
	ПараметрыЗагрузки.Диалог.ПолноеИмяФайла = НСтр("ru = '1Cv8';
													|en = '1Cv8'");
	ПараметрыЗагрузки.Диалог.Расширение     = "cf";
	ФайловаяСистемаКлиент.ЗагрузитьФайл(Оповещение, ПараметрыЗагрузки);
	
КонецПроцедуры

&НаКлиенте
Процедура Конфигуратор(Команда)
#Если Не ВебКлиент Тогда
	КомандаЗапуска = Новый Массив;
	КомандаЗапуска.Добавить(КаталогПрограммы() + "1cv8.exe");
	КомандаЗапуска.Добавить("DESIGNER");
	КомандаЗапуска.Добавить("/IBConnectionString");
	КомандаЗапуска.Добавить(СтрокаСоединенияИнформационнойБазы());
	КомандаЗапуска.Добавить("/N");
	КомандаЗапуска.Добавить(ИмяПользователя());
	
	ПараметрыЗапускаПрограммы = ФайловаяСистемаКлиент.ПараметрыЗапускаПрограммы();
	ПараметрыЗапускаПрограммы.ДождатьсяЗавершения = Ложь;
	
	ФайловаяСистемаКлиент.ЗапуститьПрограмму(КомандаЗапуска, ПараметрыЗапускаПрограммы);
#КонецЕсли
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СформироватьФайлНастроекНаСервере()
	ИмяФайлаВыгрузки = ПолучитьИмяВременногоФайла("xml");
	РеквизитФормыВЗначение("Объект").СформироватьФайлНастроекСравненияОбъединения(ИмяФайлаВыгрузки);
	ДвоичныеДанные = Новый ДвоичныеДанные(ИмяФайлаВыгрузки);
	УдалитьФайлы(ИмяФайлаВыгрузки);
	Возврат ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
КонецФункции

&НаКлиенте
Процедура ОбновитьНаИсправительнуюВерсиюПродолжение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Состояние(НСтр("ru = 'Обновление на исправительную версию...';
					|en = 'Updating to hotfix version…'"));
	ПредупрежденияПриОбновлении = ОбновитьНаИсправительнуюВерсиюНаСервере(Результат.Хранение);
	Если Не ЗначениеЗаполнено(ПредупрежденияПриОбновлении) Тогда
		Элементы.ПредупрежденияПриОбновлении.Видимость = Ложь;
		Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
		ПоказатьПредупреждение(, НСтр("ru = 'Конфигурация успешно обновлена на исправительную версию, но не применена к базе данных.
			|Следуйте дальнейшим инструкциям.';
			|en = 'The configuration has been successfully updated to the hotfix version, but it has not been applied to the database.
			|Follow further instructions.'"));
	Иначе
		Элементы.ПредупрежденияПриОбновлении.Видимость = Истина;
		Элементы.Страницы.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаПредупрежденияПриОбновлении;
		ПоказатьПредупреждение(, НСтр("ru = 'Конфигурация не обновлена на исправительную версию.
			|Отработайте предупреждения в конфигураторе или включите флажок ""Пропустить предупреждения при обновлении конфигурации"" и повторите обновление.';
			|en = 'The configuration has not been updated to the hotfix version.
			|Handle the warnings in Designer or select the ""Skip warnings during update"" checkbox and try again.'"));
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ОбновитьНаИсправительнуюВерсиюНаСервере(ВыбранныйФайлОбновления)
	ИмяФайлаОбновления = ПолучитьИмяВременногоФайла("cf");
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(ВыбранныйФайлОбновления); // ДвоичныеДанные
	ДвоичныеДанные.Записать(ИмяФайлаОбновления);
	
	ПредупрежденияПриОбновлении = РеквизитФормыВЗначение("Объект").ОбновитьНаИсправительнуюВерсию(
		ИмяФайлаОбновления, ПропуститьПредупрежденияПриОбновлении);
	
	УдалитьФайлы(ИмяФайлаОбновления);
	
	Возврат ПредупрежденияПриОбновлении;
	
КонецФункции

#КонецОбласти